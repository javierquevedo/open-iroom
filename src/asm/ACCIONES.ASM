;; -----------------------------------------------------------------------------------------------
;; -----------------------------------------------------------------------------------------------
;; ACCIONES
;; -----------------------------------------------------------------------------------------------
;; -----------------------------------------------------------------------------------------------
	
	ORG 0900H

;; -----------------------------------------------------------------------------------------------
;; RUTINA QUE EJECUTA LA ACCIÓN ASOCIADA A LA POSICIÓN ACTUAL DEL MENU (SI EXISTE)
;; UTILIZA EL BANCO DE REGISTROS 01
;; -----------------------------------------------------------------------------------------------

ACCION_UPDATE:		; PUSH_STATE
			PUSH ACC
			PUSH PSW
			PUSH DPH
			PUSH DPL
	
			SETB	RS0
			CLR	RS1
			CLR	ACCION_PEND
			LCALL	MENU_MOV_VALIDO
			MOV	R1,#25

ACCION_UPDATE_INC:	INC	DPTR
			DJNZ	R1,ACCION_UPDATE_INC
			MOV	A,#0
			MOVC	A,@A+DPTR
			MOV	R1,A
			INC	DPTR
			MOV	A,#0
			MOVC	A,@A+DPTR
			MOV	R2,A
			CJNE	R1,#0,ACCION_UPDATE_SALTO
			CJNE	R2,#0,ACCION_UPDATE_SALTO
			JMP	ACCION_UPDATE_EXIT	

ACCION_UPDATE_SALTO:	MOV	DPH,R1
			MOV	DPL,R2
			MOV	A,#0
			JMP	@A+DPTR

ACCION_UPDATE_EXIT:	SETB	RS232_PEND_MENU
			SETB	LCD_PEND_INFO
			SETB	LCD_PEND	
			POP DPL	
			POP DPH
			POP PSW
			POP ACC
			RET



ACCION_SUBIR_PERSIANA:
		JB	MOTOR2_STATUSI,ACCION_SP_PARAR
		lcall	EJECUTAR_IM2I
		JMP	ACCION_UPDATE_EXIT
ACCION_SP_PARAR:LCALL	EJECUTAR_IM2P
		CLR	MOTOR2_STATUSI
		CLR	MOTOR2_STATUSD
		LJMP	ACCION_UPDATE_EXIT
ACCION_BAJAR_PERSIANA:
		JB	MOTOR2_STATUSD,ACCION_BP_PARAR
		LCALL	EJECUTAR_IM2D
		JMP	ACCION_UPDATE_EXIT
ACCION_BP_PARAR:
		LCALL	EJECUTAR_IM2P
		CLR	MOTOR2_STATUSI
		CLR	MOTOR2_STATUSD
		LJMP	ACCION_UPDATE_EXIT
ACCION_LUZ_TECHO_ON:
		JB	LUZ_01,ACCION_LUZ_TECHO_OFF
		SETB	LUZ_01
		JMP	ACCION_UPDATE_EXIT
ACCION_LUZ_TECHO_OFF:	
		CLR	LUZ_01
		JMP	ACCION_UPDATE_EXIT 

ACCION_LUZ_ESTUDIO_ON:
		JB	LUZ_02,ACCION_LUZ_ESTUDIO_OFF
		SETB	LUZ_02
		JMP	ACCION_UPDATE_EXIT
ACCION_LUZ_ESTUDIO_OFF:	
		CLR	LUZ_02
		JMP	ACCION_UPDATE_EXIT 



ACCION_LUZ_ESTUDIO_UP:
		LCALL	EJECUTAR_IM1I
		JMP	ACCION_UPDATE_EXIT

ACCION_LUZ_ESTUDIO_DOWN:
		LCALL	EJECUTAR_IM1D	
		JMP	ACCION_UPDATE_EXIT
	

	
ACCION_ALARMA_ON_OFF:
	SETB	LCD_PEND_ALARM
	SETB	LCD_PEND
	JB	ALARMA_STATUS,AA_APAGAR
	SETB	ALARMA_STATUS
	LJMP	ACCION_ALARMA_EXIT
AA_APAGAR:
	CLR	ALARMA_STATUS
ACCION_ALARMA_EXIT:

	JMP	ACCION_UPDATE_EXIT

ACCION_A_LUZ_01:
	SETB	LCD_PEND_ALARM
	SETB	LCD_PEND
	JB	ALARMA_LUZ_01,AA_01_OFF
	SETB	ALARMA_LUZ_01
	LJMP	ACCION_ALARMA_EXIT
AA_01_OFF:	
	CLR	ALARMA_LUZ_01
	JMP	ACCION_ALARMA_EXIT

ACCION_A_LUZ_02:
	SETB	LCD_PEND_ALARM
	SETB	LCD_PEND
	JB	ALARMA_LUZ_02,AA_02_OFF
	SETB	ALARMA_LUZ_02
	LJMP	ACCION_ALARMA_EXIT
AA_02_OFF:	
	CLR	ALARMA_LUZ_02
	JMP	ACCION_ALARMA_EXIT

ACCION_A_MOTOR_01:	
	SETB	LCD_PEND_ALARM
	SETB	LCD_PEND
	JB	ALARMA_MOTOR_01,AA_M01_OFF
	SETB	ALARMA_MOTOR_01
	LJMP	ACCION_ALARMA_EXIT
AA_M01_OFF:	
	CLR	ALARMA_MOTOR_01
	JMP	ACCION_ALARMA_EXIT
ACCION_A_MOTOR_02:	
	SETB	LCD_PEND_ALARM
	SETB	LCD_PEND
	JB	ALARMA_MOTOR_02,AA_M02_OFF
	SETB	ALARMA_MOTOR_02
	LJMP	ACCION_ALARMA_EXIT
AA_M02_OFF:	
	CLR	ALARMA_MOTOR_02
	JMP	ACCION_ALARMA_EXIT
		
ACCION_ALARMA_HORAS:
	
	SETB	LCD_PEND_ALARM
	SETB	LCD_PEND
	MOV	A,ALARMA_HORA
	CJNE	A,#23,ACCION_AH_1
	MOV	ALARMA_HORA,#0
	LJMP	ACCION_AH_2	
ACCION_AH_1:
	INC	ACC
	MOV	ALARMA_HORA,A
ACCION_AH_2:
	JMP	ACCION_ALARMA_EXIT
	
ACCION_ALARMA_MINUTOS:
	
	SETB	LCD_PEND_ALARM
	SETB	LCD_PEND
	MOV	A,ALARMA_MINUTO
	CJNE	A,#59,ACCION_AM_1
	MOV	ALARMA_MINUTO,#0
	LJMP	ACCION_AM_2	
ACCION_AM_1:
	INC	ACC
	MOV	ALARMA_MINUTO,A
ACCION_AM_2:
	JMP	ACCION_ALARMA_EXIT

	
ACCION_RELOJ_HORAS:	
	CLR	TR2
	MOV	A,CLK_HORAS
	CJNE	A,#23,ACCION_RH_1
	MOV	A,#0
	LJMP	ACCION_RH_2
ACCION_RH_1:
	INC	A
ACCION_RH_2:
	MOV	CLK_HORAS,A
	SETB	TR2
	SETB	LCD_PEND_TIME
	SETB	LCD_PEND
	LJMP	ACCION_UPDATE_EXIT
	
ACCION_RELOJ_MINUTOS:
	CLR	TR2
	MOV	A,CLK_MIN
	CJNE	A,#59,ACCION_RM_1
	MOV	A,#0
	LJMP	ACCION_RM_2
ACCION_RM_1:
	INC	A
ACCION_RM_2:
	MOV	CLK_MIN,A
	SETB	TR2
	MOV	CLK_COUNT,#20
	MOV	CLK_SEG,#0
	SETB	LCD_PEND_TIME
	SETB	LCD_PEND
	LJMP	ACCION_UPDATE_EXIT	
	LJMP	ACCION_UPDATE_EXIT	